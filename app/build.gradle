apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.github.rstockbridge.maisitotracker"
        minSdkVersion 27
        //noinspection OldTargetApi
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        buildConfigField "String", "CONSUMER_KEY", "\"$maisitoTrackerConsumerKey\""
        buildConfigField "String", "CONSUMER_SECRET", "\"$maisitoTrackerConsumerSecret\""
        buildConfigField "String", "ACCESS_TOKEN", "\"$maisitoTrackerAccessToken\""
        buildConfigField "String", "ACCESS_TOKEN_SECRET", "\"$maisitoTrackerAccessTokenSecret\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "DRY_RUN", "true"
            buildConfigField "int", "HIGH_PRESSURE_TRIGGER_DURATION_S", "10"
            buildConfigField "long", "COOLDOWN_M", "1"
        }

        deploy {
            initWith debug
            buildConfigField "boolean", "DRY_RUN", "false"
            buildConfigField "int", "HIGH_PRESSURE_TRIGGER_DURATION_S", "180"
            buildConfigField "long", "COOLDOWN_M", "60"
        }
    }

    variantFilter { variant ->
        if (variant.buildType.name == 'release') {
            variant.setIgnore(true)
        }
    }

    lintOptions {
        disable('AllowBackup', 'GoogleAppIndexingWarning', 'MissingApplicationIcon')
    }
}

dependencies {
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'org.twitter4j:twitter4j-core:4.0.7'
    compileOnly 'com.google.android.things:androidthings:1.0'
}
